buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'io.freefair.gradle:aspectj-plugin:6.6.3'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.5'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'io.freefair.aspectj.post-compile-weaving' version '6.6.3'
    id 'checkstyle'
}

group = 'com.github'
version = "0.6.4"

compileJava {
    sourceCompatibility = '17'
    targetCompatibility = '17'
}

bootJar {
    archiveFileName = "checkit-server-${project.version}.jar"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

processResources {
    filesMatching('application.yml') {
        expand(['version': project.version])
    }
}

repositories {
    mavenCentral()
}

ext {
    jopaVersion = '0.22.0'
    jopaSpringTransactionVersion = '0.1.6'
    springVersion = '3.0.6'
    keycloakVersion = '21.1.1'
    jenaVersion = '4.8.0'
    githubApiVersion = '1.314'
}

dependencies {
    aspect "cz.cvut.kbss.jopa:jopa-impl:$jopaVersion"

    implementation "org.springframework.boot:spring-boot-starter-websocket:$springVersion"
    implementation "org.springframework.boot:spring-boot-starter-security:$springVersion"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-client:$springVersion"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server:$springVersion"
    implementation "org.springframework.boot:spring-boot-starter-validation:$springVersion"
    implementation "cz.cvut.kbss.jopa:jopa-impl:$jopaVersion"
    implementation "cz.cvut.kbss.jopa:ontodriver-rdf4j:$jopaVersion"
    implementation "com.github.ledsoft:jopa-spring-transaction:$jopaSpringTransactionVersion"
    implementation "org.keycloak:keycloak-admin-client:$keycloakVersion"
    implementation "org.apache.jena:apache-jena-libs:$jenaVersion"
    implementation "org.kohsuke:github-api:$githubApiVersion"

    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"

    testImplementation "org.springframework.boot:spring-boot-starter-test:$springVersion"
    testImplementation "org.springframework.security:spring-security-test:6.0.2"
}

checkstyle {
    toolVersion '10.8.1'
    configFile file("config/checkstyle/checkstyle.xml")
}

checkstyleMain {
    source = 'src/main/java'
}

checkstyleTest {
    source = 'src/test/java'
}

tasks.named('test') {
    useJUnitPlatform()
}
